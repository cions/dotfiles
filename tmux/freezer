#!/bin/zsh

usage() {
    print -u2 "usage: freezer {toggle,query} {pane,emerge,www-browser}"
    exit 1
}

cmd=${argv[1]}
target=${argv[2]}

case ${target} {
    (pane)
        pane_pid="$(tmux display -p "#{pane_pid}")"
        [[ -f /proc/${pane_pid}/comm ]] || exit 1
        while [[ "$(</proc/${pane_pid}/comm)" == "sudo" ]] {
            pane_pid="$(pgrep -P ${pane_pid})"
        }
        [[ -f /proc/${pane_pid}/cgroup ]] || exit 1
        for cgroup in "${(@f)$(</proc/${pane_pid}/cgroup)}"; {
            subsys=${cgroup[(ws.:.)2]}
            hier=${cgroup[(ws.:.)3]}
            if [[ ",${subsys}," != *,freezer,* ]] continue
            if [[ ${hier} != /shell/* ]] continue
            statefile=/sys/fs/cgroup/${subsys}${hier}/freezer.state
            break
        }
        ;;
    (emerge)
        statefile=/sys/fs/cgroup/freezer/emerge/freezer.state
        ;;
    (www-browser)
        statefile=/sys/fs/cgroup/freezer/www-browser/freezer.state
        ;;
    (*)
        usage
        ;;
}

[[ -f ${statefile} ]] || exit 1

read -r state < ${statefile}
case ${cmd} {
    (toggle)
        if [[ ${state} == FROZEN ]] {
            print THAWED > ${statefile}
        } else {
            print FROZEN > ${statefile}
        }
        ;;
    (query)
        if [[ ${state} == FROZEN ]] {
            print -nr -- ${argv[3]-FROZEN}
        } else {
            print -nr -- ${argv[4]-THAWED}
        }
        ;;
    (*)
        usage
        ;;
}
