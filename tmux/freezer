#!/bin/zsh

function usage() {
    print -u2 "usage: freezer {toggle,query} {pane,emerge,www-browser}"
    exit 1
}

cmd=$1
target=$2

case ${target} in
    pane)
        pane_pid="$(tmux display -p "#{pane_pid}")"
        if [[ "$(tmux display -p "#{pane_start_command}")" == "sudo -i" ]]; then
            pane_pid="$(pgrep -P ${pane_pid})"
        fi
        [[ -f /proc/${pane_pid}/cgroup ]] || exit 1
        for cgroup in "${(@f)$(</proc/${pane_pid}/cgroup)}"; do
            subsys=${cgroup[(ws.:.)2]}
            hier=${cgroup[(ws.:.)3]}
            [[ ",${subsys}," == *,freezer,* ]] || continue
            [[ ${hier} == /shell/* ]] || continue
            statefile=/sys/fs/cgroup/${subsys}${hier}/freezer.state
            break
        done
        ;;
    emerge)
        statefile=/sys/fs/cgroup/freezer/emerge/freezer.state
        ;;
    www-browser)
        statefile=/sys/fs/cgroup/freezer/www-browser/freezer.state
        ;;
    *)
        usage
        ;;
esac

[[ -f ${statefile} ]] || exit 1

state="$(<${statefile})"
case ${cmd} in
    toggle)
        if [[ ${state} == FROZEN ]]; then
            print THAWED > ${statefile}
        else
            print FROZEN > ${statefile}
        fi
        ;;
    query)
        if [[ ${state} == FROZEN ]]; then
            print -nr ${3-FROZEN}
        else
            print -nr ${4-THAWED}
        fi
        ;;
    *)
        usage
        ;;
esac
