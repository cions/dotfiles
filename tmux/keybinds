#!/bin/zsh

tmux-if-chain() {
    if (( $# > 1 )) {
        print -rn -- "if -Ft= ${(qqq)argv[1]} ${(qqq)argv[2]}" \
            "${(qqq)$(tmux-if-chain "${(@)argv[3,-1]}")}"
    } else {
        print -rn -- ${argv[1]}
    }
}

tmux bind-key -n WheelUpPane select-pane -t= "\;" \
    ${(Q)${(z)"$(tmux-if-chain \
    "#{mouse_any_flag}" "send-keys -M" \
    "#{pane_in_mode}" "send-keys -M; send-keys -M; send-keys -M" \
    "#{alternate_on}" "send-keys Up Up Up" \
    "copy-mode -e; send-keys -M; send-keys -M; send-keys -M")"}}

tmux bind-key -n WheelDownPane select-pane -t= "\;" \
    ${(Q)${(z)"$(tmux-if-chain \
    "#{mouse_any_flag}" "send-keys -M" \
    "#{pane_in_mode}" "send-keys -M; send-keys -M; send-keys -M" \
    "#{alternate_on}" "send-keys Down Down Down" "")"}}

if (( ${+commands[xsel]} )) {
    tmux bind-key -T copy-mode-vi y send -X copy-pipe-and-cancel 'xsel -l /dev/null -ib'
    tmux bind-key p run "xsel -ob | tmux load-buffer -; tmux paste-buffer -r"
} elif (( ${+commands[clip]} )) {
    tmux bind-key -T copy-mode-vi y send -X copy-pipe-and-cancel 'clip'
    tmux bind-key p run "paste | tmux load-buffer -; tmux paste-buffer -r"
}
if (( ${+commands[chromium]} )) {
    tmux bind-key -T copy-mode-vi o send -X copy-pipe-and-cancel 'xargs -0 chromium'
} elif (( ${+commands[chrome]} )) {
    tmux bind-key -T copy-mode-vi o send -X copy-pipe-and-cancel 'xargs -0 chrome'
}
if (( ${+commands[xdg-open]} )) {
    tmux bind-key -T copy-mode-vi C-o send -X copy-pipe-and-cancel 'cd "#{pane_current_path}"; xargs -0 xdg-open'
} elif (( ${+commands[start]} )) {
    tmux bind-key -T copy-mode-vi C-o send -X copy-pipe-and-cancel 'cd "#{pane_current_path}"; xargs -0 start'
}
