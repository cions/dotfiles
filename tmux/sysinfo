#!/bin/zsh

separator() {
    local oldcolor=$1
    local newcolor=$2

    if [[ ${newcolor} == ${oldcolor} ]]; then
        print -n "\u2502"
    else
        print -n "#[fg=${oldcolor} bg=${newcolor}]\u258C#[fg=default bg=${newcolor}]"
    fi
}

meminfo=( "${(@f)$(</proc/meminfo)}" )
(( total = ${meminfo[(r)MemTotal:*][(w)2]:-0} ))
(( used = total - ${meminfo[(r)MemAvailable:*][(w)2]:-0} ))
(( percentage = 100.0 * used / total ))

if (( percentage >= 80.0 )); then
    color="colour203" # red
elif (( percentage >= 60.0 )); then
    color="colour227" # yellow
else
    color="colour156" # green
fi

print -n "#[fg=default bg=${color}]"
print -nf " %s#{?#{e|>=:#{client_width},120}, (%.1f%%),} " "$(numfmt --from-unit=Ki --to=iec ${used})" ${percentage}

(( total = ${meminfo[(r)SwapTotal:*][(w)2]:-0} ))
(( used = total - ${meminfo[(r)SwapFree:*][(w)2]:-0} ))
(( percentage = 100.0 * used / total ))

if (( total == 0 )); then
    newcolor="colour242" # gray
elif (( percent >= 80.0 )); then
    newcolor="colour203" # red
elif (( percent >= 60.0 )); then
    newcolor="colour227" # yellow
else
    newcolor="colour156" # green
fi
separator ${color} ${newcolor}
color=${newcolor}

if (( total == 0 )); then
    print -n " #[fg=colour253]N/A#[fg=default] "
else
    print -nf " %s#{?#{e|>=:#{client_width},120}, (%.1f%%),} " "$(numfmt --from-unit=Ki --to=iec ${used})" ${percentage}
fi

loadavg=( "${(s: :)$(</proc/loadavg)[1,(w)3]}" )
nproc="$(nproc --all)"

if (( loadavg[1] / nproc >= 1.0 )); then
    newcolor="colour203" # red
elif (( loadavg[1] / nproc >= 0.5 )); then
    newcolor="colour227" # yellow
else
    newcolor="colour156" # green
fi
separator ${color} ${newcolor}
color=${newcolor}

print -n " #{?#{e|>=:#{client_width},120},${(j: :)loadavg},${loadavg[1]}} "
