#!/bin/zsh

memusage() {
    local meminfo total used free percent color

    meminfo=( "${(@f)$(</proc/meminfo)}" )
    (( total = ${meminfo[(r)MemTotal:*][(w)2]:-0} ))
    (( free = ${meminfo[(r)MemFree:*][(w)2]:-0} ))
    (( free += ${meminfo[(r)Buffers:*][(w)2]:-0} ))
    (( free += ${meminfo[(r)Cached:*][(w)2]:-0} ))
    (( used = total - free ))
    (( percent = used * 100.0 / total ))

    if (( percent >= 80.0 )) {
        color="red"
    } elif (( percent >= 60.0 )) {
        color="yellow"
    } else {
        color="green"
    }
    if [[ $1 != "-s" ]] {
        print -nf "%s:ÔÉñ%4s/%4s (%.2f%%)" \
            ${color} \
            "$(numfmt --from-unit=Ki --to=iec ${used})" \
            "$(numfmt --from-unit=Ki --to=iec ${total})" \
            ${percent}
    } else {
        print -nr "${color}:ÔÉñ$(numfmt --from-unit=Ki --to=iec ${used})"
    }
}

swapusage() {
    local meminfo total used percent color

    meminfo=( "${(@f)$(</proc/meminfo)}" )
    (( total = ${meminfo[(r)SwapTotal:*][(w)2]:-0} ))
    (( used = total - ${meminfo[(r)SwapFree:*][(w)2]:-0} ))
    (( percent = used * 100.0 / total ))

    if (( total == 0 )) {
        color="gray2"
    } elif (( percent >= 80.0 )) {
        color="red"
    } elif (( percent >= 60.0 )) {
        color="yellow"
    } else {
        color="green"
    }
    if [[ $1 != "-s" ]] {
        print -nf "%s:ÔÇñ%4s/%4s (%.2f%%)" \
            ${color} \
            "$(numfmt --from-unit=Ki --to=iec ${used})" \
            "$(numfmt --from-unit=Ki --to=iec ${total})" \
            ${percent}
    } else {
        print -nr "${color}:ÔÇñ$(numfmt --from-unit=Ki --to=iec ${used})"
    }
}

loadavg() {
    local loadavg nproc color

    loadavg=( "${(s: :)$(</proc/loadavg)[1,(w)3]}" )
    nproc="$(nproc --all)"

    if (( loadavg[1] / nproc >= 1.0 )) {
        color="red"
    } elif (( loadavg[1] / nproc >= 0.5 )) {
        color="yellow"
    } else {
        color="green"
    }
    if [[ $1 != "-s" ]] {
        print -nr "${color}:ÔÅΩ${(j: :)loadavg}"
    } else {
        print -nr "${color}:ÔÅΩ${loadavg[1]}"
    }
}

width="$(tmux display -p "#{client_width}")"
segments=()
case "$1" {
    (left)
        segments+=( "cyan:#S" "cyan:#H" )
        frozen=""
        frozen+="$(~/.tmux/freezer query pane "üÑø" "")"
        frozen+="$(~/.tmux/freezer query emerge "üÑ¥" "")"
        frozen+="$(~/.tmux/freezer query www-browser "üÖÜ" "")"
        if [[ -n ${frozen} ]] segments+=( "orange:${frozen}" )
        powerprompt -f tmux -L -- "${segments[@]}"
        ;;
    (right)
        if (( width >= 130 )) {
            segments+=( "$(memusage)" )
        } elif (( width >= 96 )) {
            segments+=( "$(memusage -s)" )
        }
        if (( width >= 145 )) {
            segments+=( "$(swapusage)" )
        } elif (( width >= 104 )) {
            segments+=( "$(swapusage -s)" )
        }
        if (( width >= 114 )) {
            segments+=( "$(loadavg)" )
        } else {
            segments+=( "$(loadavg -s)" )
        }
        if (( width >= 80 )) {
            segments+=( "cyan:ÔÅ®${(%):-"%D{%Y-%m-%d}"}" )
        }
        if (( width >= 70 )) {
            segments+=( "cyan:ÔÅÜ${(%):-"%D{%H:%M}"}" )
        }
        powerprompt -f tmux -R -- "${segments[@]}"
        ;;
    (window)
        powerprompt -f tmux -n -L -- "gray1:" "gray2: #I:#W#F "
        ;;
    (cwindow)
        powerprompt -f tmux -n -L -- "gray1:" "green: #I:#W#F "
        ;;
}
