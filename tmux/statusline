#!/bin/zsh

typeset -A icons

if (( ENABLE_ICONS )) {
    POWERPROMPT="${HOME}/.bin/powerprompt -f tmux"
    icons=(
        [memusage]=""
        [swapusage]=""
        [loadavg]=""
        [date]=""
        [time]=""
    )
} else {
    POWERPROMPT="${HOME}/.bin/powerprompt -P -f tmux"
}

memusage() {
    local meminfo total used percentage color

    meminfo=( "${(@f)$(</proc/meminfo)}" )
    (( total = ${meminfo[(r)MemTotal:*][(w)2]:-0} ))
    (( used = total - ${meminfo[(r)MemAvailable:*][(w)2]:-0} ))
    (( percentage = 100.0 * used / total ))

    if (( percentage >= 80.0 )) {
        color="red"
    } elif (( percentage >= 60.0 )) {
        color="yellow"
    } else {
        color="green"
    }
    if [[ $1 != "-s" ]] {
        print -nf "${color}:${icons[memusage]}%4s/%4s (%.2f%%)" \
            "$(numfmt --from-unit=Ki --to=iec ${used})" \
            "$(numfmt --from-unit=Ki --to=iec ${total})" \
            ${percentage}
    } else {
        print -nr "${color}:${icons[memusage]}$(numfmt --from-unit=Ki --to=iec ${used})"
    }
}

swapusage() {
    local meminfo total used percentage color

    meminfo=( "${(@f)$(</proc/meminfo)}" )
    (( total = ${meminfo[(r)SwapTotal:*][(w)2]:-0} ))
    (( used = total - ${meminfo[(r)SwapFree:*][(w)2]:-0} ))
    (( percentage = 100.0 * used / total ))

    if (( total == 0 )) {
        color="gray2"
    } elif (( percent >= 80.0 )) {
        color="red"
    } elif (( percent >= 60.0 )) {
        color="yellow"
    } else {
        color="green"
    }
    if [[ $1 != "-s" ]] {
        print -nf "${color}:${icons[swapusage]}%4s/%4s (%.2f%%)" \
            "$(numfmt --from-unit=Ki --to=iec ${used})" \
            "$(numfmt --from-unit=Ki --to=iec ${total})" \
            ${percentage}
    } else {
        print -nr "${color}:${icons[swapusage]}$(numfmt --from-unit=Ki --to=iec ${used})"
    }
}

loadavg() {
    local loadavg nproc color

    loadavg=( "${(s: :)$(</proc/loadavg)[1,(w)3]}" )
    nproc="$(nproc --all)"

    if (( loadavg[1] / nproc >= 1.0 )) {
        color="red"
    } elif (( loadavg[1] / nproc >= 0.5 )) {
        color="yellow"
    } else {
        color="green"
    }
    if [[ $1 != "-s" ]] {
        print -nr "${color}:${icons[loadavg]}${(j: :)loadavg}"
    } else {
        print -nr "${color}:${icons[loadavg]}${loadavg[1]}"
    }
}

position=${argv[1]}
segments=()
case ${position} {
    (left)
        segments+=( "cyan:#S" "cyan:#H" )
        ${=POWERPROMPT} -L -- "${segments[@]}"
        ;;
    (right)
        width="$(tmux display -p "#{client_width}")"
        (( maxwidth = width - $(tmux display -p " #S   #H  #{W:#{?#{<=:#I,3},  #I:#W#F  ,}}" | wc -c) ))
        (( maxwidth = (maxwidth > 0.6 * width) ? 0.6 * width : maxwidth ))
        (( curwidth = 90 ))
        typeset -A style=( [memusage]="full" [swapusage]="full" [loadavg]="full" [date]="full" [time]="full" )
        if [[ ! -r /proc/loadavg ]] { style[loadavg]="hidden"; (( curwidth -= 19 )) }
        if (( curwidth > maxwidth )) { style[swapusage]="short"; (( curwidth -= 14 )) }
        if (( curwidth > maxwidth )) { style[memusage]="short"; (( curwidth -= 14 )) }
        if [[ -r /proc/loadavg ]] && (( curwidth > maxwidth )) { style[loadavg]="short"; (( curwidth -= 10 )) }
        if (( curwidth > maxwidth )) { style[date]="hidden"; (( curwidth -= 15 )) }
        if (( curwidth > maxwidth )) { style[swapusage]="hidden"; (( curwidth -= 9 )) }
        if (( curwidth > maxwidth )) { style[memusage]="hidden"; (( curwidth -= 9 )) }
        if (( curwidth > maxwidth )) { style[time]="hidden"; (( curwidth -= 10 )) }

        if [[ ${style[memusage]} == "full" ]] {
            segments+=( "$(memusage)" )
        } elif [[ ${style[memusage]} == "short" ]] {
            segments+=( "$(memusage -s)" )
        }
        if [[ ${style[swapusage]} == "full" ]] {
            segments+=( "$(swapusage)" )
        } elif [[ ${style[swapusage]} == "short" ]] {
            segments+=( "$(swapusage -s)" )
        }
        if [[ ${style[loadavg]} == "full" ]] {
            segments+=( "$(loadavg)" )
        } elif [[ ${style[loadavg]} == "short" ]] {
            segments+=( "$(loadavg -s)" )
        }
        if [[ ${style[date]} == "full" ]] {
            segments+=( "cyan:${icons[date]}${(%):-"%D{%Y-%m-%d}"}" )
        }
        if [[ ${style[time]} == "full" ]] {
            segments+=( "cyan:${icons[time]}${(%):-"%D{%H:%M}"}" )
        }
        ${=POWERPROMPT} -R -- "${segments[@]}"
        ;;
    (window)
        ${=POWERPROMPT} -n -L -- "gray1:" "gray2: #I:#W#F "
        ;;
    (cwindow)
        ${=POWERPROMPT} -n -L -- "gray1:" "green: #I:#W#F "
        ;;
}
