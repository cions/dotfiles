#!/bin/zsh

typeset -A icons

if (( ENABLE_ICONS )) {
    POWERPROMPT="$HOME/.bin/powerprompt -f tmux"
    icons=(
        [memusage]=""
        [swapusage]=""
        [loadavg]=""
        [date]=""
        [time]=""
    )
} else {
    POWERPROMPT="$HOME/.bin/powerprompt -P -f tmux"
}

memusage() {
    local meminfo total used percentage color

    meminfo=( "${(@f)$(</proc/meminfo)}" )
    (( total = ${meminfo[(r)MemTotal:*][(w)2]:-0} ))
    (( used = total - ${meminfo[(r)MemAvailable:*][(w)2]:-0} ))
    (( percentage = used * 100.0 / total ))

    if (( percentage >= 80.0 )) {
        color="red"
    } elif (( percentage >= 60.0 )) {
        color="yellow"
    } else {
        color="green"
    }
    if [[ $1 != "-s" ]] {
        print -nf "${color}:${icons[memusage]}%4s/%4s (%.2f%%)" \
            "$(numfmt --from-unit=Ki --to=iec ${used})" \
            "$(numfmt --from-unit=Ki --to=iec ${total})" \
            ${percentage}
    } else {
        print -nr "${color}:${icons[memusage]}$(numfmt --from-unit=Ki --to=iec ${used})"
    }
}

swapusage() {
    local meminfo total used percentage color

    meminfo=( "${(@f)$(</proc/meminfo)}" )
    (( total = ${meminfo[(r)SwapTotal:*][(w)2]:-0} ))
    (( used = total - ${meminfo[(r)SwapFree:*][(w)2]:-0} ))
    (( percentage = used * 100.0 / total ))

    if (( total == 0 )) {
        color="gray2"
    } elif (( percent >= 80.0 )) {
        color="red"
    } elif (( percent >= 60.0 )) {
        color="yellow"
    } else {
        color="green"
    }
    if [[ $1 != "-s" ]] {
        print -nf "${color}:${icons[swapusage]}%4s/%4s (%.2f%%)" \
            "$(numfmt --from-unit=Ki --to=iec ${used})" \
            "$(numfmt --from-unit=Ki --to=iec ${total})" \
            ${percentage}
    } else {
        print -nr "${color}:${icons[swapusage]}$(numfmt --from-unit=Ki --to=iec ${used})"
    }
}

loadavg() {
    local loadavg nproc color

    loadavg=( "${(s: :)$(</proc/loadavg)[1,(w)3]}" )
    nproc="$(nproc --all)"

    if (( loadavg[1] / nproc >= 1.0 )) {
        color="red"
    } elif (( loadavg[1] / nproc >= 0.5 )) {
        color="yellow"
    } else {
        color="green"
    }
    if [[ $1 != "-s" ]] {
        print -nr "${color}:${icons[loadavg]}${(j: :)loadavg}"
    } else {
        print -nr "${color}:${icons[loadavg]}${loadavg[1]}"
    }
}

position=${argv[1]}
width="$(tmux display -p "#{client_width}")"
segments=()
case ${position} {
    (left)
        segments+=( "cyan:#S" "cyan:#H" )
        ${=POWERPROMPT} -L -- "${segments[@]}"
        ;;
    (right)
        if (( width >= 130 )) {
            segments+=( "$(memusage)" )
        } elif (( width >= 96 )) {
            segments+=( "$(memusage -s)" )
        }
        if (( width >= 145 )) {
            segments+=( "$(swapusage)" )
        } elif (( width >= 104 )) {
            segments+=( "$(swapusage -s)" )
        }
        if [[ -r /proc/loadavg ]] {
            if (( width >= 114 )) {
                segments+=( "$(loadavg)" )
            } else {
                segments+=( "$(loadavg -s)" )
            }
        }
        if (( width >= 80 )) {
            segments+=( "cyan:${icons[date]}${(%):-"%D{%Y-%m-%d}"}" )
        }
        if (( width >= 70 )) {
            segments+=( "cyan:${icons[time]}${(%):-"%D{%H:%M}"}" )
        }
        ${=POWERPROMPT} -R -- "${segments[@]}"
        ;;
    (window)
        ${=POWERPROMPT} -n -L -- "gray1:" "gray2: #I:#W#F "
        ;;
    (cwindow)
        ${=POWERPROMPT} -n -L -- "gray1:" "green: #I:#W#F "
        ;;
}
