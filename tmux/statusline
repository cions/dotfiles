#!/bin/zsh

typeset -A icons

if (( ENABLE_ICONS )); then
    POWERPROMPT="${HOME}/.bin/powerprompt -f tmux"
    icons=(
        [memusage]=""
        [swapusage]=""
        [loadavg]=""
        [date]=""
        [time]=""
    )
else
    POWERPROMPT="${HOME}/.bin/powerprompt -P -f tmux"
fi

memusage() {
    local meminfo total used percentage color

    meminfo=( "${(@f)$(</proc/meminfo)}" )
    (( total = ${meminfo[(r)MemTotal:*][(w)2]:-0} ))
    (( used = total - ${meminfo[(r)MemAvailable:*][(w)2]:-0} ))
    (( percentage = 100.0 * used / total ))

    if (( percentage >= 80.0 )); then
        color="red"
    elif (( percentage >= 60.0 )); then
        color="yellow"
    else
        color="green"
    fi
    if [[ $1 != "-s" ]]; then
        print -nf "${color}:${icons[memusage]}%4s/%4s (%.2f%%)" \
            "$(numfmt --from-unit=Ki --to=iec ${used})" \
            "$(numfmt --from-unit=Ki --to=iec ${total})" \
            ${percentage}
    else
        print -nr "${color}:${icons[memusage]}$(numfmt --from-unit=Ki --to=iec ${used})"
    fi
}

swapusage() {
    local meminfo total used percentage color

    meminfo=( "${(@f)$(</proc/meminfo)}" )
    (( total = ${meminfo[(r)SwapTotal:*][(w)2]:-0} ))
    (( used = total - ${meminfo[(r)SwapFree:*][(w)2]:-0} ))
    (( percentage = 100.0 * used / total ))

    if (( total == 0 )); then
        color="gray2"
    elif (( percent >= 80.0 )); then
        color="red"
    elif (( percent >= 60.0 )); then
        color="yellow"
    else
        color="green"
    fi
    if [[ $1 != "-s" ]]; then
        print -nf "${color}:${icons[swapusage]}%4s/%4s (%.2f%%)" \
            "$(numfmt --from-unit=Ki --to=iec ${used})" \
            "$(numfmt --from-unit=Ki --to=iec ${total})" \
            ${percentage}
    else
        print -nr "${color}:${icons[swapusage]}$(numfmt --from-unit=Ki --to=iec ${used})"
    fi
}

loadavg() {
    local loadavg nproc color

    loadavg=( "${(s: :)$(</proc/loadavg)[1,(w)3]}" )
    nproc="$(nproc --all)"

    if (( loadavg[1] / nproc >= 1.0 )); then
        color="red"
    elif (( loadavg[1] / nproc >= 0.5 )); then
        color="yellow"
    else
        color="green"
    fi
    if [[ $1 != "-s" ]]; then
        print -nr "${color}:${icons[loadavg]}${(j: :)loadavg}"
    else
        print -nr "${color}:${icons[loadavg]}${loadavg[1]}"
    fi
}

position=${argv[1]}
segments=()
case ${position} in
    left)
        segments+=( "cyan:#S" "cyan:#H" )
        ${=POWERPROMPT} -L -- "${segments[@]}"
        ;;
    right)
        width="$(tmux display -p "#{client_width}")"
        (( maxwidth = width - $(tmux display -p " #S   #H  #{W:#{?#{<=:#I,3},  #I:#W#F  ,}}" | wc -c) ))
        (( maxwidth = (maxwidth > 0.6 * width) ? 0.6 * width : maxwidth ))
        (( curwidth = 90 ))
        typeset -A style=( [memusage]="full" [swapusage]="full" [loadavg]="full" [date]="full" [time]="full" )
        [[ -r /proc/loadavg ]] || { style[loadavg]="hidden"; (( curwidth -= 19 )) }
        (( curwidth > maxwidth )) && { style[swapusage]="short"; (( curwidth -= 14 )) }
        (( curwidth > maxwidth )) && { style[memusage]="short"; (( curwidth -= 14 )) }
        [[ -r /proc/loadavg ]] && (( curwidth > maxwidth )) && { style[loadavg]="short"; (( curwidth -= 10 )) }
        (( curwidth > maxwidth )) && { style[date]="hidden"; (( curwidth -= 15 )) }
        (( curwidth > maxwidth )) && { style[swapusage]="hidden"; (( curwidth -= 9 )) }
        (( curwidth > maxwidth )) && { style[memusage]="hidden"; (( curwidth -= 9 )) }
        (( curwidth > maxwidth )) && { style[time]="hidden"; (( curwidth -= 10 )) }

        if [[ ${style[memusage]} == "full" ]]; then
            segments+=( "$(memusage)" )
        elif [[ ${style[memusage]} == "short" ]]; then
            segments+=( "$(memusage -s)" )
        fi
        if [[ ${style[swapusage]} == "full" ]]; then
            segments+=( "$(swapusage)" )
        elif [[ ${style[swapusage]} == "short" ]]; then
            segments+=( "$(swapusage -s)" )
        fi
        if [[ ${style[loadavg]} == "full" ]]; then
            segments+=( "$(loadavg)" )
        elif [[ ${style[loadavg]} == "short" ]]; then
            segments+=( "$(loadavg -s)" )
        fi
        if [[ ${style[date]} == "full" ]]; then
            segments+=( "cyan:${icons[date]}${(%):-"%D{%Y-%m-%d}"}" )
        fi
        if [[ ${style[time]} == "full" ]]; then
            segments+=( "cyan:${icons[time]}${(%):-"%D{%H:%M}"}" )
        fi
        ${=POWERPROMPT} -R -- "${segments[@]}"
        ;;
    window)
        ${=POWERPROMPT} -n -L -- "gray1:" "gray2: #I:#W#F "
        ;;
    cwindow)
        ${=POWERPROMPT} -n -L -- "gray1:" "green: #I:#W#F "
        ;;
esac
