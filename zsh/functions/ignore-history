#autoload

emulate -L zsh

local tokens=( ${(z)argv[1]%$'\n'} )

if [[ ${tokens[-1]} == --help(|[-=]*) ]] return 1
if [[ ${tokens[-1]} == "--version" ]] return 1

if (( ${(M)#reswords:#${tokens[1]}} )) return 0
if (( ${+aliases[${tokens[1]}]} )) return 0
if [[ ${tokens[1]} == '(' ]] return 0        # ( subshell )
if [[ ${tokens[1]} == '()' ]] return 0       # () { unnamed function }
if [[ ${tokens[2]} == '()' ]] return 0       # func() { function definition }
if [[ ${tokens[1]} == \(\(*\)\) ]] return 0  # (( arithmetic expression ))

if [[ -o EQUALS && ${tokens[1]} == "="* ]] {
    return $(( ! ${+commands[${(Qe)tokens[1]#=}]} ))
}

local cmd=${(Qe)tokens[1]}
if (( ${+commands[${cmd}]} )) return 0
if (( ${+functions[${cmd}]} )) return 0
if (( ${+builtins[${cmd}]} )) return 0
if [[ ${cmd} == */* && -x ${cmd} ]] return 0

return 1
