# vim: set ft=zsh:

emulate -L zsh
setopt extended_glob

autoload -Uz match-words-by-style

local curcontext=":zle:${WIDGET}" word done
local -a matched_words
integer count=${NUMERIC:-1}

if (( count < 0 )) {
    zle -l -a ${WIDGET/backward-} && zle -n $((-count)) -w ${WIDGET/backward-}
    return
}

while (( count-- )) {
    match-words-by-style

    if [[ ${matched_words[3]} == [[:space:]]##[^[:space:]]* ]] {
        match-words-by-style -w space
    }

    word="${matched_words[2]}${matched_words[3]}"
    if [[ -n ${word} ]] {
        if (( done )) || [[ ${LASTWIDGET} == *kill* ]] {
            CUTBUFFER="${word}${CUTBUFFER}"
        } else {
            zle copy-region-as-kill -- "${word}"
        }
        LBUFFER="${matched_words[1]}"
    } else {
        return 1
    }
    done=1
}

zle -f 'kill'

return 0
